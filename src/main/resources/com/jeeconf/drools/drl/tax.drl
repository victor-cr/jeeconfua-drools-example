package com.jeeconf.drools.drl
dialect "mvel"

import java.math.BigDecimal;

import com.jeeconf.drools.bean.*;

global org.slf4j.Logger log;

// START Prefetch

rule "Fetch all parties"
when
    TransactionRecord( $party : party )
    not( Party( name == $party.name ) )
then
    insert( $party );
end

rule "Fetch all counter parties"
when
    TransactionRecord( $party : counterParty )
    not( Party( name == $party.name ) )
then
    insert( $party );
end

// END Prefetch

rule "Fixed tax - category I"
when
    $party : Taxpayer( category == Category.I )
then
    insertLogical( new TaxRecord( $party, 100 ) )
end

rule "Fixed tax - category II"
when
    $party : Taxpayer( category == Category.II )
then
    insertLogical( new TaxRecord( $party, 200 ) )
end

rule "Percentage tax - category III"
when
    $party : Taxpayer( category == Category.III )
    $transaction : TransactionRecord( party == $party, amount > 0, $amount : amount )
then
    insertLogical( new TaxRecord( $party, $amount * 0.05 ) )
end

rule "Percentage tax - general case"
when
    $party : Taxpayer( category == Category.NONE )
    $transaction : TransactionRecord( party == $party, $amount : amount )
then
    insert( new TaxRecord( $party, $amount * 0.17 ) )
end

// Limits

rule "Limit exceeded - category I"
when
    $party : Taxpayer( category == Category.I )
    BigDecimal( plus > 150000) // 150 K
           from accumulate( TransactionRecord( party == $party, amount > 0, $amount : amount ),
                            init( BigDecimal sum = 0; ),
                            action( sum += $amount; ),
                            result( sum ) )
then
    modify( $party ) {
        category = Category.NONE;
    }
end

rule "Limit exceeded - category II"
when
    $party : Taxpayer( category == Category.II )
    BigDecimal( plus > 1000000) // 1 M
           from accumulate( TransactionRecord( party == $party, amount > 0, $amount : amount ),
                            init( BigDecimal sum = 0; ),
                            action( sum += $amount; ),
                            result( sum ) )
then
    modify( $party ) {
        category = Category.NONE;
    }
end

rule "Limit exceeded - category III"
when
    $party : Taxpayer( category == Category.III )
    BigDecimal( plus > 3000000) // 3 M
           from accumulate( TransactionRecord( party == $party, amount > 0, $amount : amount ),
                            init( BigDecimal sum = 0; ),
                            action( sum += $amount; ),
                            result( sum ) )
then
    modify( $party ) {
        category = Category.NONE;
    }
end

